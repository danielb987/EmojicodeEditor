🕊 🍔 🍇
  🍰 name 🍬🔡
  🍰 ingredients 🍨🐚🔡

  🐈 🆕 🍼 name 🍬🔡 🍼 ingredients 🍨🐚🔡 🍇
  🍉

  🐖 😀 🍇
    🍊🍦 name name 🍇
      😀 🍪 🔤Burger name: 🔤 name 🍪
    🍉
    😀 🔷🔡🍨 ingredients 🔤, 🔤
  🍉
🍉

🕊 🍾 🍇
  🍰 hotness 🚀
  🍰 flavor 🔡
  🍰 amount 🚂

  🐈 🆕 🍼 hotness 🚀 🍼 flavor 🔡 🍼 amount 🚂 🍇
  🍉

  🐖 😀 🍇
    😀 🍪 🔤Sauce 🔤 flavor 🔤 🔤 🔡 amount 10 🔤ml Hot: 🔤 🔡 hotness 10 🍪
  🍉
🍉

🐇 📃 🍇
  🍰 burger 🍔
  🍰 drink 🍬🔡
  🍰 sauce 🍬🍾

  🐈 🆕 🍼 burger 🍔 🍼 drink 🍬🔡 🍼 sauce 🍬🍾 🍇
  🍉

  🐖 😀 🍇
    😀 burger
    🍊🍦 drink drink 🍇
      😀 🍪 🔤Drink: 🔤 drink 🍪
    🍉
    🍊🍦 sauce sauce 🍇
      😀 sauce
    🍉
  🍉
🍉

🏁 🍇
  🍦 orders 🔷🍨🐚📃🐸

  🔂 i ⏩ 0 5000000 🍇
    🍦 a 🔷📃🆕 🔷🍔🆕 🍪🔤Double Bacon🔤 🔡 i 10🍪 🍨 🔤Bacon🔤 🔤Tomato🔤 🍆 ⚡️  🔷🍾🆕 0.3 🍪🔤barbecue🔤 🔡 i 10🍪 12
    🍦 b 🔷📃🆕 🔷🍔🆕 🍪🔤Triple Burger🔤 🔡 i 10🍪  🍨 🔤Onion🔤 🔤Salad🔤 🔤Tomato🔤 🍆 🔤Coke🔤 🔷🍾🆕 1.0 🍪🔤chilli🔤 🔡 i 10🍪 20
    🍦 c 🔷📃🆕 🔷🍔🆕 🍪🔤Cheeseburger🔤 🔡 i 10🍪  🍨 🔤Cheese🔤 🍆 🔤Lemonade🔤 ⚡️
    🍦 d 🔷📃🆕 🔷🍔🆕 🍪🔤Standard Burger🔤 🔡 i 10🍪  🍨 🔤Egg🔤 🔤Salad🔤 🔤Bacon🔤 🔤Cucumber🔤 🍆 🔤Coke🔤 ⚡️
    🍊 😛 🚮 i 200000 0 🍇
      🐻 orders a
    🍉
    🍊 😛 🚮 i 300000 0 🍇
      🐻 orders b
    🍉
    🍊 😛 🚮 i 700000 0 🍇
      🐻 orders c
    🍉
    🍊 😛 🚮 i 500000 0 🍇
      🐻 orders d
    🍉
  🍉

  🔂 o orders 🍇
    😀 o
  🍉
🍉
